import groovy.json.JsonSlurper

plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def dartDefine = [
    WEBTRIT_APP_ID_SUFFIX           : '',
    WEBTRIT_APP_NAME                : 'WebTrit',
    WEBTRIT_ANDROID_RELEASE_KEY_PATH: '',
]

if (project.hasProperty('dart-defines')) {
    dartDefine += project.property('dart-defines')
            .split(',')
            .collectEntries { entry ->
                def pair = new String(entry.decodeBase64(), 'UTF-8').split('=')
                if (pair.size() > 1) {
                    [(pair.first()): pair.last()]
                } else {
                    [(pair.first()): '']
                }
            }
}

def keystoreJsonFile = rootProject.file(dartDefine.WEBTRIT_ANDROID_RELEASE_KEY_PATH + "key.json")
def keystoreJKSFile = rootProject.file(dartDefine.WEBTRIT_ANDROID_RELEASE_KEY_PATH + "upload-keystore.jks")

println("Used keys to keystore by path: $keystoreJsonFile\nUsed keystore by path: $keystoreJKSFile\n")

def keystoreJsonObject = new JsonSlurper().parse(keystoreJsonFile)

android {
    namespace "com.webtrit.app"
    compileSdkVersion 33 // flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.webtrit.app"
        applicationIdSuffix dartDefine.WEBTRIT_APP_ID_SUFFIX
        minSdkVersion 23 // flutter.minSdkVersion
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        resValue "string", "APP_NAME", dartDefine.WEBTRIT_APP_NAME
    }

    signingConfigs {
        release {
            keyAlias keystoreJsonObject.keyAlias
            keyPassword keystoreJsonObject.keyPassword
            storeFile keystoreJKSFile
            storePassword keystoreJsonObject.storePassword
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source '../..'
}

dependencies {}
